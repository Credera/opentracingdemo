apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'eclipse'
apply plugin: 'idea'

mainClassName = "com.credera.demo.opentracing.cross_section.Application"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.2.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.2.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/../proto/java/src/gen"
}

jar {
    baseName = 'cross-section'
    version = '0.1.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['../proto/java/src/gen/main/grpc', '../proto/java/src/gen/main/java', 'src/main/java']
        }
    }
}

dependencies {
    compile 'io.grpc:grpc-netty:1.2.0'
    compile 'io.grpc:grpc-protobuf:1.2.0'
    compile 'io.grpc:grpc-stub:1.2.0'
    compile 'io.opentracing.brave:brave-opentracing:0.19.2'
    compile 'io.zipkin.reporter:zipkin-sender-okhttp3:0.6.13'
    testCompile 'junit:junit:4.12'
}

